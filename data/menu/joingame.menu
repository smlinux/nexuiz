// Property of Alientrap/AK
//
// joingame.menu

Item Window JoinGame
{
	Derive Picture Table1
	{
		picture "gfx/join_border"
		size	'910 610'
	}
	Derive Picture Table2
	{
		picture "gfx/join_background"
		pos	'28 32'
		size	'854 481'
	}
	Item Layout JoinGame
	{
		size		'872 501'
		direction	'0 8'
		alignment	[AlignCenter]
		origin		'460 44'

		#define SList_Ping	'50 15'
		#define SList_Name	'467 15'
		#define SList_Map	'120 15'
		#define SList_Players	'70 15'
		#define SList_Mod	'90 15'
		#define SList_Direction '2 0'

		Derive Nex_Composition Query
		{
			Derive TextButton Description
			{
				normal		"Server name filter:"

				link		"##next"
			}
			Derive EditBox EditBox
			{
				size	'610 12'
				target	"::Data::ServerList::QueryString::Text"

				action	Nex_Action_ExecuteQuery
			}
		}

		Item Layout TableDesc
		{
			direction	[SList_Direction]
			flag		[FlagEmbedded]
			size		'844 15'

			DeriveTemplate Rect Theme
			{
				color	'0.5 0.5 0.5'
				alpha	0.5
				flag	[FlagEmbedded]
			}
			Derive Theme Ping
			{
				size	[SList_Ping]
				Derive TextButton Text
				{
					normal		"Ping"

					init 		Nex_Action_JoinGame_SortBy
					target		"ping"
				}
			}
			Derive Theme Name
			{
				size	[SList_Name]
				Derive TextButton Text
				{
					normal		"Server Name"

					init 		Nex_Action_JoinGame_SortBy
					target		"name"
				}
			}
			Derive Theme Map
			{
				size	[SList_Map]
				Derive TextButton Text
				{
					normal		"Map Name"

					init 		Nex_Action_JoinGame_SortBy
					target		"map"
				}
			}
			Derive Theme Players
			{
				size	[SList_Players]
				Item Arrangement Glue
				{
					direction	'0.001 0 0'
					flag		[FlagEmbedded]

					Derive TextButton NumPlayers
					{
						normal		"NP"

						init 		Nex_Action_JoinGame_SortBy
						target		"numplayers"
					}
					Derive Label	Slash
					{
						text		"/"
					}
					Derive TextButton MaxPlayers
					{
						normal		"MP"

						init 		Nex_Action_JoinGame_SortBy
						target		"maxplayers"
					}
				}
			}
			Derive Theme Mod
			{
				size	[SList_Mod]
				Derive TextButton Text
				{
					normal		"Mod Name"

					init 		Nex_Action_JoinGame_SortBy
					target		"mod"
				}
			}
		}
		Item Window Scroll
		{
			flag	[FlagEmbedded]
			size	'844 416'

			Item ScrollWindow ScrollArea
			{
				flag	[FlagEmbedded]
				size 	'824 416'

				Item Arrangement Slist
				{
					direction	'0 1 0'
					flag		[FlagEmbedded]

					DeriveTemplate Nex_HostCache_Entry Entry
					{
						size		'824 15'
						action		Nex_Action_EntryConnect

						DeriveTemplate Nex_HostCache_ValueField Ping
						{
							pos		'0 0'
							size		[SList_Ping]
							target		"ping"
							alignment	[AlignRight]
						}
						DeriveTemplate Nex_HostCache_StringField Name
						{
							pos		'52 0'
							size		[SList_Name]
							target		"name"
						}
						DeriveTemplate Nex_HostCache_StringField Map
						{
							pos		'519 0'
							size		[SList_Map]
							target		"map"
						}
						DeriveTemplate Nex_HostCache_Players Players
						{
							pos		'641 0'
							size		[SList_Players]
						}
						DeriveTemplate Nex_HostCache_StringField Mod
						{
							pos		'713 0'
							size		[SList_Mod]
							target		"mod"
						}
					}
					Item Automation_Job Generator
					{
						action Nex_Automation_CreateEntries
					}
				}
			}
			Item VScrollBar Scroller
			{
				pos	'814 0'
				size	'20 100'
				target	"##prev"
			}
		}
	}
	Derive PictureButton Refresh
	{
		pos	'395 522'
		normal	"$gfx/join_b_refresh"

		action 	Nex_Action_RefreshSlist
	}
}
